buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id "java"
    id 'org.jetbrains.kotlin.jvm' version "1.3.70"
    id 'io.codearte.nexus-staging' version '0.21.2'
    id 'maven-publish'
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.bmuschko.nexus'

group = "com.github.nkb03"
version = "0.1.1-SNAPSHOT"
archivesBaseName = 'reaktive'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }}

dokka {
    outputDirectory = "docs"
    jdkVersion = 8
    includeNonPublic = false
    noStdlibLink = false
}


task sourcesJar_(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar_(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar_
            artifact javadocJar_
        }
    }
}

modifyPom {
    project {
        name 'reaktive'
        description 'An FRP library for Kotlin'
        url 'https://github.com/nkb03/reaktive'
        inceptionYear '2020'

        scm {
            url 'https://github.com/nkb03/reaktive'
            connection 'git:https://github.com/nkb03/reaktive.git'
            developerConnection 'git:git://github.com/nkb03/reaktive.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'santaclaus'
                name 'Nikolaus Knop'
                email 'niko.knop003@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

closeRepository.dependsOn(uploadArchives)

static String kotlin(Object dep) {
    return "org.jetbrains.kotlin:kotlin-$dep:1.3.70"
}

static String spek(Object dep) {
    return "org.jetbrains.spek:spek-$dep:1.1.5"
}

dependencies {
    implementation kotlin('stdlib-jdk8')
    implementation kotlin('reflect')
    implementation 'com.github.nkb03:kref:0.1-SNAPSHOT'
    testImplementation spek('api')
    testImplementation spek('junit-platform-engine')
    testImplementation 'com.natpryce:hamkrest:1.5.0.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
