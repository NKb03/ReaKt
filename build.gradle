buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:0.9.17")
    }
}

plugins {
    id "java"
    id 'org.jetbrains.kotlin.jvm' version "1.3.11"
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'

group = "org.nikok"
version = "1.2-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.kotlin
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

dokka {
    outputDirectory = "docs"
    jdkVersion = 8
    includeNonPublic = false
    noStdlibLink = false
}

static String kotlin(Object dep) {
    return "org.jetbrains.kotlin:kotlin-$dep:1.3.11"
}

static String spek(Object dep) {
    return "org.jetbrains.spek:spek-$dep:1.1.5"
}

static String nikok(Object dep, Object version) {
    return "org.nikok:$dep:$version"
}

dependencies {
    implementation(kotlin("stdlib-jdk8"))
    implementation("org.nikok:kref:1.0.0") {
        exclude(group: "org.jetbrains.kotlin")
    }
    testImplementation(spek("api")) {
        exclude(group: "org.jetbrains.kotlin")
    }
    testImplementation(spek("junit-platform-engine")) {
        exclude(group: "org.jetbrains.kotlin")
    }
    testImplementation("com.natpryce:hamkrest:1.5.0.0")
    implementation(kotlin("reflect"))
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
